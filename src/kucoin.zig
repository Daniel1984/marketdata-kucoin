const std = @import("std");
const ws = @import("websocket");
const zimq = @import("zimq");
const request = @import("./request.zig");
const types = @import("./types.zig");
const relay = @import("marketdata_relay_pub");
const json = std.json;
const crypto = std.crypto;

pub const Self = @This();

allocator: std.mem.Allocator,
token: ?[]u8,
endpoint: ?[]u8,
ping_interval: u64,
ping_timeout: u64,
mutex: std.Thread.Mutex,
client: ?ws.Client,
stream: relay.Self,
subscription_topics: ?[]const []const u8,

pub fn init(allocator: std.mem.Allocator, stream: relay.Self) !Self {
    return Self{
        .allocator = allocator,
        .token = null,
        .endpoint = null,
        .ping_interval = 18000,
        .ping_timeout = 10000,
        .mutex = std.Thread.Mutex{},
        .client = null,
        .stream = stream,
        .subscription_topics = null,
    };
}

pub fn deinit(self: *Self) void {
    if (self.token) |token| {
        self.allocator.free(token);
        self.token = null;
    }
    if (self.endpoint) |endpoint| {
        self.allocator.free(endpoint);
        self.endpoint = null;
    }
    self.deinitClient();
}

fn deinitClient(self: *Self) void {
    if (self.client) |*client| {
        client.deinit();
        self.client = null;
    }
}

pub fn getSocketConnectionDetails(self: *Self) !void {
    if (!self.mutex.tryLock()) return;
    defer self.mutex.unlock();

    const body = try request.post(self.allocator, "https://api.kucoin.com/api/v1/bullet-public");
    defer self.allocator.free(body);

    const parsedBody = try json.parseFromSlice(types.KuCoinTokenResponse, self.allocator, body, .{ .ignore_unknown_fields = true });
    defer parsedBody.deinit();

    if (!std.mem.eql(u8, parsedBody.value.code, "200000")) return error.ConnectionError;
    if (parsedBody.value.data.instanceServers.len == 0) return error.MissingInstanceServers;

    // Free existing allocations before creating new ones
    if (self.token) |token| {
        self.allocator.free(token);
        self.token = null;
    }
    if (self.endpoint) |endpoint| {
        self.allocator.free(endpoint);
        self.endpoint = null;
    }

    // Use temporary variables to avoid partial state on error
    const token = try self.allocator.dupe(u8, parsedBody.value.data.token);
    errdefer self.allocator.free(token);

    const endpoint = try self.allocator.dupe(u8, parsedBody.value.data.instanceServers[0].endpoint);
    errdefer self.allocator.free(endpoint);

    // Only assign after both succeed
    self.token = token;
    self.endpoint = endpoint;
    self.ping_interval = parsedBody.value.data.instanceServers[0].pingInterval;
    self.ping_timeout = parsedBody.value.data.instanceServers[0].pingTimeout;

    std.log.info("token: {s}", .{self.token.?});
    std.log.info("endpoint: {s}", .{self.endpoint.?});
}

pub fn connectWebSocket(self: *Self) !void {
    const uri = try std.Uri.parse(self.endpoint.?);

    // Extract host safely
    const host_component = uri.host.?;
    const host = switch (host_component) {
        .raw => |raw| raw,
        .percent_encoded => |encoded| encoded,
    };

    const port: u16 = uri.port orelse if (std.mem.eql(u8, uri.scheme, "wss")) 443 else 80;
    const is_tls = std.mem.eql(u8, uri.scheme, "wss");

    // Clean up existing client if it exists
    if (self.client) |*client| {
        client.deinit();
        self.client = null;
    }

    self.client = try ws.Client.init(self.allocator, .{
        .port = port,
        .host = host,
        .tls = is_tls,
        .max_size = 4096,
        .buffer_size = 1024,
    });

    const request_path = try std.fmt.allocPrint(self.allocator, "/?token={s}", .{self.token.?});
    defer self.allocator.free(request_path);

    const headers = try std.fmt.allocPrint(self.allocator, "Host: {s}", .{host});
    defer self.allocator.free(headers);

    try self.client.?.handshake(request_path, .{
        .timeout_ms = 10000,
        .headers = headers,
    });

    std.log.info("socket connection established!", .{});
}

pub fn subscribe(self: *Self, topics: []const []const u8) !void {
    self.subscription_topics = topics;
    try self.subscribeChannel();
}

pub fn subscribeChannel(self: *Self) !void {
    if (self.subscription_topics == null) {
        return error.NoSubscriptionTopics;
    }

    for (self.subscription_topics.?, 0..) |topic, i| {
        const subscribe_msg = types.SubscribeMessage{
            .id = i,
            .type = "subscribe",
            .topic = topic,
            .response = true,
        };

        const subscribe_json = try std.fmt.allocPrint(self.allocator, "{f}", .{std.json.fmt(subscribe_msg, .{})});
        defer self.allocator.free(subscribe_json);

        std.log.info("subscription payload: {s}", .{subscribe_json});
        try self.client.?.write(subscribe_json);
    }
}

fn reconnect(self: *Self) !void {
    self.deinitClient();

    var retry_count: u32 = 0;
    const max_retries = 5;
    var backoff_ms: u64 = 1000;

    while (retry_count < max_retries) {
        std.log.info("reconnection attempt {}/{}", .{ retry_count + 1, max_retries });

        if (retry_count > 0) {
            std.Thread.sleep(backoff_ms * std.time.ns_per_ms);
            backoff_ms *= 2;
        }

        self.connectWebSocket() catch |err| {
            std.log.err("failed to connect websocket: {}", .{err});
            retry_count += 1;
            continue;
        };

        self.subscribeChannel() catch |err| {
            std.log.err("failed to subscribe channel: {}", .{err});
            retry_count += 1;
            continue;
        };

        std.log.info("reconnected successfully after {} attempts", .{retry_count + 1});
        return;
    } else {
        std.log.err("failed to reconnect after {} attempts", .{max_retries});
        return error.ReconnectionFailed;
    }
}

pub fn consume(self: *Self) !void {
    std.log.info("starting message consumption", .{});

    // Initialize ping timer outside the main loop to maintain timing across reconnections
    var ping_timer = try std.time.Timer.start();

    while (true) {
        // Set a read timeout
        self.client.?.readTimeout(2000) catch |err| {
            std.log.err("failed to set read timeout: {} - attempting reconnection", .{err});
            try self.reconnect();
            ping_timer.reset(); // Reset timer after reconnection
            continue;
        };

        // Handle incoming messages
        const ping_interval_ns = self.ping_interval * std.time.ns_per_ms;

        // Check if we need to send a ping
        if (ping_timer.read() >= ping_interval_ns) {
            var ping_data = [_]u8{};
            self.client.?.writePing(&ping_data) catch |err| {
                std.log.err("failed to send ping: {} - attempting reconnection", .{err});
                try self.reconnect();
                ping_timer.reset(); // Reset timer after reconnection
                continue;
            };
            std.log.info("Sent ping", .{});
            ping_timer.reset();
        }

        const message = self.client.?.read() catch |err| {
            std.log.err("read error: {} - attempting reconnection", .{err});
            try self.reconnect();
            ping_timer.reset(); // Reset timer after reconnection
            continue;
        };

        if (message) |msg| {
            defer self.client.?.done(msg);

            switch (msg.type) {
                .text => {
                    // std.log.info("Received: {s}", .{msg.data});

                    const parsed = std.json.parseFromSlice(std.json.Value, self.allocator, msg.data, .{}) catch |err| {
                        std.log.warn("Failed to parse message as JSON: {}", .{err});
                        continue;
                    };
                    defer parsed.deinit();

                    if (parsed.value.object.get("type")) |msg_type| {
                        switch (msg_type) {
                            .string => |type_str| {
                                if (std.mem.eql(u8, type_str, "welcome")) {
                                    std.log.info("✓ Welcome message received", .{});
                                    continue;
                                }

                                if (std.mem.eql(u8, type_str, "ack")) {
                                    std.log.info("✓ Subscription acknowledged", .{});
                                    continue;
                                }

                                if (std.mem.eql(u8, type_str, "message")) {
                                    // Transform Kucoin response data to match common format
                                    const transformed_data = self.transformOrderbookData(msg.data) catch |err| {
                                        std.log.warn("failed to transform orderbook data: {}", .{err});
                                        continue;
                                    };
                                    defer self.allocator.free(transformed_data);

                                    // std.debug.print("::: transformed_data :> {s}\n", .{transformed_data});
                                    self.stream.publishMessage(transformed_data) catch |err| {
                                        std.log.warn("failed publishing msg: {}", .{err});
                                    };
                                    continue;
                                }

                                if (std.mem.eql(u8, type_str, "error")) {
                                    std.log.err("❌ WebSocket error: {s}", .{msg.data});
                                }
                            },
                            else => {},
                        }
                    }
                },
                .binary => {
                    std.log.info("Received binary message of {} bytes", .{msg.data.len});
                },
                .ping => {
                    std.log.info("Received ping", .{});
                    const pong_data = try self.allocator.dupe(u8, msg.data);
                    defer self.allocator.free(pong_data);
                    try self.client.?.writePong(pong_data);
                },
                .pong => {
                    std.log.info("Received pong", .{});
                },
                .close => {
                    std.log.info("WebSocket connection closed by server", .{});
                    try self.client.?.close(.{});
                    break;
                },
            }
        }
    }

    std.log.info("WebSocket connection closed", .{});
}

fn transformOrderbookData(self: *Self, original_data: []const u8) ![]u8 {
    const parsed = std.json.parseFromSlice(std.json.Value, self.allocator, original_data, .{}) catch |err| {
        std.log.warn("Failed to parse Bybit message for transformation: {}", .{err});
        return err;
    };
    defer parsed.deinit();

    // Clone the original structure
    var root_obj = std.json.ObjectMap.init(self.allocator);
    defer root_obj.deinit();

    var pair_string: ?[]u8 = null;
    defer if (pair_string) |p| self.allocator.free(p);

    try root_obj.put("src", std.json.Value{ .string = "kucoin" });
    try root_obj.put("type", std.json.Value{ .string = "orderbook" });

    if (parsed.value.object.get("timestamp")) |ts| {
        try root_obj.put("ts", ts);
    }

    if (parsed.value.object.get("topic")) |topic| {
        if (topic == .string) {
            const topic_str = topic.string;
            if (std.mem.indexOf(u8, topic_str, ":")) |colon_index| {
                const pair_with_dash = topic_str[colon_index + 1 ..];
                const pair = try std.mem.replaceOwned(u8, self.allocator, pair_with_dash, "-", "");
                pair_string = pair;
                try root_obj.put("pair", std.json.Value{ .string = pair });
            }
        }
    }

    if (parsed.value.object.get("data")) |data_value| {
        var data_obj = std.json.ObjectMap.init(self.allocator);
        defer data_obj.deinit();

        if (data_value.object.get("bids")) |b| {
            try root_obj.put("bids", b);
        }

        if (data_value.object.get("asks")) |a| {
            try root_obj.put("asks", a);
        }
    }

    const root_json_value = std.json.Value{ .object = root_obj };
    return try std.fmt.allocPrint(self.allocator, "{f}", .{std.json.fmt(root_json_value, .{})});
}
